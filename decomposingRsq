sysuse auto, clear

gen y = mpg

*** Estimate linear regression for non-foreign cars.
reg y price weight if foreign == 0
predict yhat0

*** Estimate linear regression for foreign cars.
reg y price weight if foreign == 1
predict yhat1

collapse (sd) y yhat1 yhat0, by(foreign)

*** Generate variance of y.
replace y = y^2

forvalues i = 0/1{
	replace yhat`i' = yhat`i'^2
	gen rsq`i' = yhat`i' / y
	rename yhat`i' yhat_`i'x_
	rename rsq`i' rsq`i'x_
}

gen id = 1
reshape wide y* rsq*, i(id) j(foreign)

*** Observed R-squared for sample 0 and sample 1.
gen obsrsq0 = rsq0x_0
gen obsrsq1 = rsq1x_1

*** comp1: structural change.
gen comp1 = ln(yhat_1x_1) - ln(yhat_0x_1)

*** comp2: structural change.
gen comp2 = ln(yhat_0x_1) - ln(yhat_0x_0)

*** comp3: structural change.
gen comp3 = ln(y0) - ln(y1)

*** Check: sum of comps is the same with log of observed R-sq?
gen logrsq = ln(rsq1x_1 / rsq0x_0)
gen sumcomp = comp1 + comp2 + comp3

tabstat logrsq sumcomp comp1 comp2 comp3
